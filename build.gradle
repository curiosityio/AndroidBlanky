// Top-level build file where you can add configuration options common to all sub-projects/modules.

ext {
    rootPackageName = "com.levibostian.androidblanky"

    // allows developer to set the minSdk via gradle command line during development. API 21+ are faster.
    // so use 21+ during dev, then prod builds will still be built with 16 as don't have command line.
    def minSdk = hasProperty('minSdk') ? minSdk : 16
    minSdkVersion = minSdk
    targetSdkVersion = 26
    compileSdkVersion = 26
    buildToolsVersion = '26.0.1'

    // App dependencies
    supportLibraryVersion = '26.0.2'
    architectureComponentsVersion = "1.0.0-alpha9"
    rxandroidVersion = "2.0.1"
    rxjavaVersion = "2.1.0"
    rxkotlinVersion = "2.1.0"
    rxbindingVersion = "2.0.0"
    moshiVersion = "1.5.0"
    retrofitVersion = "2.3.0"
    daggerVersion = "2.8"
    realmAdaptersVersion = "2.1.0"
    crashlyticsVersion = "2.6.1"
    junitVersion = '4.12'
    mockitoVersion = '1.10.19'
    dexmakerVersion = "1.2"
    hamcrestVersion = '1.3'
    runnerVersion = '0.5'
    rulesVersion = '0.5'
    espressoVersion = '2.2.2'
}

buildscript {
    ext.isCi = Boolean.parseBoolean(System.getProperty('CI', 'false'))

    ext.kotlin_version = '1.1.3-2'
    repositories {
        maven { url 'https://maven.google.com' }
        jcenter()
        mavenCentral()
        maven { url 'https://plugins.gradle.org/m2' }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.0.0-beta4'
        classpath "io.realm:realm-gradle-plugin:3.7.0"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'net.ltgt.gradle:gradle-errorprone-plugin:0.0.11'

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

allprojects {
    repositories {
        maven { url "https://jitpack.io" }
        google()
        jcenter()
    }

    configurations.all {
        resolutionStrategy {
            eachDependency { details ->
                // Force all of the primary support libraries to use the same version.
                if (details.requested.group == 'com.android.support'
                        && details.requested.name != 'multidex'
                        && details.requested.name != 'multidex-instrumentation') {
                    details.useVersion supportLibraryVersion
                }
                // Force all the error-prone dependencies to use the same version.
                if (details.requested.group == 'com.google.errorprone'
                        && details.requested.name.startsWith('error_prone_')) {
                    details.useVersion versions.errorProne
                }
            }
        }
    }

    if (isCi) {
        project.apply(plugin: 'net.ltgt.errorprone')

        project.tasks.withType(JavaCompile) {
            options.compilerArgs += [
                    '-Xep:UnnecessaryDefaultInEnumSwitch:ERROR',
                    '-Xep:TypeParameterUnusedInFormals:ERROR',
                    '-Xep:MissingOverride:ERROR',
                    '-Xep:ClassCanBeStatic:ERROR',
                    '-Xep:OperatorPrecedence:ERROR',
                    '-Xep:ReferenceEquality:ERROR',
                    '-Xep:FloatingPointLiteralPrecision:ERROR',
                    '-Xep:SimpleDateFormatConstant:ERROR',
            ]
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
