buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
        mavenCentral()
    }
    dependencies {
        classpath 'io.fabric.tools:gradle:1.25.4' // latest version is found here: https://s3.amazonaws.com/fabric-artifacts/public/io/fabric/tools/gradle/maven-metadata.xml
    }
}
apply plugin: 'com.android.application'
apply plugin: 'com.google.firebase.firebase-perf'
apply plugin: 'io.fabric'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

repositories {
    maven { url 'http://s3.amazonaws.com/fabric-artifacts/public' }
    mavenCentral()
    google()
    jcenter()
}

android {
    compileSdkVersion 28
    buildToolsVersion "28.0.0"

    packagingOptions {
        exclude 'META-INF/ASL2.0'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
    }

    defaultConfig {
        applicationId "com.levibostian.androidblanky"
        // allows developer to set the minSdk via gradle command line **during development**. API 21+ are faster. `./gradlew assembleDevelopmentDebug -PminSdk=23`
        minSdkVersion hasProperty('minSdk') ? minSdk : 16
        targetSdkVersion 28
        versionCode 1528215515
        versionName "0.1.0"

        testInstrumentationRunner 'com.levibostian.androidblanky.AndroidTestTestRunner'

        multiDexEnabled true
        vectorDrawables.useSupportLibrary = true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    signingConfigs {
        releaseSigning {
            storeFile rootProject.file('keystores/keystore-name.keystore')
            storePassword 'password'
            keyAlias 'android-blanky'
            keyPassword 'password'
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            
            signingConfig signingConfigs.releaseSigning
        }
    }

    flavorDimensions "mode"
    productFlavors {
        development {
            dimension "mode"
            applicationIdSuffix ".debug"
        }
        production {
            dimension "mode"
        }
        beta {
            dimension "mode"
            applicationIdSuffix ".beta"
        }
    }
    testOptions {
        animationsDisabled = true
    }
    kapt {
        arguments {
            arg("room.schemaLocation", "$projectDir/schemas".toString())
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    // Support libraries
    def supportLibraryVersion = "28.0.0-alpha3"
    implementation "com.android.support:appcompat-v7:$supportLibraryVersion"
    implementation "com.android.support:design:$supportLibraryVersion"
    implementation "com.android.support:recyclerview-v7:$supportLibraryVersion"
    implementation "com.android.support:preference-v7:$supportLibraryVersion"

    // Teller
    implementation 'com.github.levibostian:teller-android:0.0.1'

    // Wendy
    implementation 'com.github.levibostian:Wendy-Android:0.2.1-alpha'

    // Firebase
    implementation "com.google.firebase:firebase-core:16.0.0"
    implementation 'com.google.firebase:firebase-perf:16.0.0'
    implementation 'com.google.firebase:firebase-messaging:17.0.0'
    implementation 'com.google.firebase:firebase-invites:16.0.0'

    // Play Install Referrer Library
    implementation 'com.android.installreferrer:installreferrer:1.0'

    // Custom views
    implementation "com.github.levibostian:androidviews:0.1.4"

    // Architecture components
    def lifecycleVersion = "1.1.1"
    implementation "android.arch.lifecycle:extensions:$lifecycleVersion"
    kapt "android.arch.lifecycle:compiler:$lifecycleVersion"
    implementation "android.arch.lifecycle:reactivestreams:$lifecycleVersion"
    testImplementation "android.arch.core:core-testing:$lifecycleVersion"

    // Room
    def roomVersion = "1.1.0"
    implementation "android.arch.persistence.room:runtime:$roomVersion"
    kapt "android.arch.persistence.room:compiler:$roomVersion"
    implementation "android.arch.persistence.room:rxjava2:$roomVersion"
    testImplementation "android.arch.persistence.room:testing:$roomVersion"

    // Dagger
    def daggerVersion = "2.8"
    kapt "com.google.dagger:dagger-compiler:$daggerVersion"
    implementation "com.google.dagger:dagger:$daggerVersion"

    // RxJava
    implementation "io.reactivex.rxjava2:rxandroid:2.0.2"
    implementation "io.reactivex.rxjava2:rxjava:2.1.13"
    implementation "com.f2prateek.rx.preferences2:rx-preferences:2.0.0"

    // Moshi
    def moshiVersion = "1.6.0"
    implementation "com.squareup.moshi:moshi:$moshiVersion"
    kapt "com.squareup.moshi:moshi-kotlin-codegen:$moshiVersion"
    implementation "com.squareup.moshi:moshi-adapters:$moshiVersion"

    // Retrofit
    def retrofitVersion = "2.4.0"
    implementation "com.squareup.retrofit2:retrofit:$retrofitVersion"
    implementation "com.squareup.retrofit2:converter-moshi:$retrofitVersion"
    implementation "com.squareup.retrofit2:adapter-rxjava2:$retrofitVersion"
    implementation "com.squareup.retrofit2:retrofit-mock:$retrofitVersion"

    // OkHttp
    def okHttpVersion = "3.10.0"
    implementation "com.squareup.okhttp3:logging-interceptor:$okHttpVersion"

    // Licenses fragment
    implementation 'com.artit-k:license-fragment-support-v4:1.3.0'

    // Eventbus
    implementation "org.greenrobot:eventbus:3.0.0"

    // Crashlytics
    implementation 'com.crashlytics.sdk.android:crashlytics:2.9.3'

    // Timber
    implementation "com.jakewharton.timber:timber:4.7.0"

    // JUnit
    def junitVersion = "4.12"
    testImplementation "junit:junit:$junitVersion"
    androidTestImplementation "junit:junit:$junitVersion"

    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"
    testImplementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"
    testImplementation "org.jetbrains.kotlin:kotlin-test-junit:$kotlinVersion"

    // Truth
    def truthVersion = "0.40"
    testImplementation "com.google.truth:truth:$truthVersion"
    androidTestImplementation "com.google.truth:truth:$truthVersion"

    // Mockito
    def mockitoVersion = "2.18.3"
    def mockitoKotlinVersion = "1.5.0"
    testImplementation "org.mockito:mockito-core:$mockitoVersion"
    testImplementation "com.nhaarman:mockito-kotlin-kt1.1:$mockitoKotlinVersion", { exclude group: 'org.jetbrains.kotlin' }
    androidTestImplementation "org.mockito:mockito-android:$mockitoVersion"
    androidTestImplementation "com.nhaarman:mockito-kotlin-kt1.1:$mockitoKotlinVersion", { exclude group: 'org.jetbrains.kotlin' }

    // Android Testing Support Library's runner and rules
    def androidTestLibVersion = "1.0.2"
    androidTestImplementation "com.android.support.test:runner:$androidTestLibVersion"
    androidTestImplementation "com.android.support.test:rules:$androidTestLibVersion"

    // Espresso UI Testing
    def espressoVersion = "3.0.2"
    androidTestImplementation "com.android.support.test.espresso:espresso-core:$espressoVersion"
    androidTestImplementation "com.android.support.test.espresso:espresso-contrib:$espressoVersion"
    androidTestImplementation "com.android.support.test.espresso:espresso-intents:$espressoVersion"
    androidTestImplementation "com.android.support.test.espresso:espresso-accessibility:$espressoVersion"
    androidTestImplementation "com.android.support.test.espresso:espresso-web:$espressoVersion"
    androidTestImplementation "com.android.support.test.espresso.idling:idling-concurrent:$espressoVersion"
    androidTestImplementation "com.android.support.test.espresso:espresso-idling-resource:$espressoVersion"

    // Fastlane screengrab
    androidTestImplementation "tools.fastlane:screengrab:1.1.0"
}

configurations.all {
    resolutionStrategy {
        force "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"
    }
}

apply plugin: 'com.google.gms.google-services'
