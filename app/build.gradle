apply plugin: 'com.android.application'
if (System.getenv("RELEASE") == "true") {
    apply plugin: 'com.google.firebase.firebase-perf'
}

apply plugin: 'io.fabric'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: "kotlin-allopen"

androidExtensions {
    experimental = true
}

allOpen {
    // allows mocking for classes w/o directly opening them for release builds
    annotation 'com.levibostian.androidblanky.testing.OpenClass'
}

repositories {
    maven { url 'http://s3.amazonaws.com/fabric-artifacts/public' }
    mavenCentral()
    google()
    jcenter()
}

android {
    compileSdkVersion 28
    // Make sure to also update .travis.yml, and fastlane/Fastfile build tools version values.
    buildToolsVersion "28.0.3"

    packagingOptions {
        exclude 'META-INF/ASL2.0'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
    }

    defaultConfig {
        applicationId "com.levibostian.androidblanky"
        minSdkVersion 16
        targetSdkVersion 28
        versionCode 1528215515
        versionName "0.1.0"

        testInstrumentationRunner 'com.levibostian.androidblanky.AndroidTestTestRunner'

        multiDexEnabled true
        vectorDrawables.useSupportLibrary = true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    signingConfigs {
        releaseSigning {
            storeFile rootProject.file('keystores/keystore-name.keystore')
            storePassword 'password'
            keyAlias 'android-blanky'
            keyPassword 'password'
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            
            signingConfig signingConfigs.releaseSigning
        }
    }

    flavorDimensions "mode"
    productFlavors {
        development {
            // Set dev only rules to skip compiling some parts of app.
            minSdkVersion 23
            resConfigs "en", "xxhdpi"

            versionNameSuffix "-dev"
            dimension "mode"
            applicationIdSuffix ".dev"
        }
        beta {
            dimension "mode"
            applicationIdSuffix ".beta"
            versionNameSuffix "-beta"
        }
        production {
            dimension "mode"
        }
    }
    testOptions {
        animationsDisabled = true
    }
    kapt {
        arguments {
            arg("room.schemaLocation", "$projectDir/schemas".toString())
        }
    }
    sourceSets {
        androidTest.assets.srcDirs += files("$projectDir/schemas".toString())

        // Put code into below directory that you want shared between unit and integration tests.
        String sharedTestDir = 'src/sharedTest/java'
        test.java.srcDirs += sharedTestDir
        androidTest.java.srcDirs += sharedTestDir
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    // AndroidX UI libs
    implementation 'androidx.core:core-ktx:1.0.1'
    implementation "androidx.appcompat:appcompat:1.0.2"
    implementation "androidx.fragment:fragment:1.0.0"
    implementation "androidx.fragment:fragment-ktx:1.0.0"
    debugImplementation 'androidx.fragment:fragment-testing:1.1.0-alpha05'
    implementation "com.google.android.material:material:1.0.0"
    implementation "androidx.recyclerview:recyclerview:1.0.0"
    implementation "androidx.preference:preference:1.0.0"
    // Required to support SDK < 21
    implementation 'com.android.support:multidex:1.0.3'

    // Teller
    implementation 'com.levibostian.teller:teller-android:0.2.0-alpha'

    // Wendy
    implementation 'com.github.levibostian:Wendy-Android:b14c06c7f0'

    // WorkManager
    def work_version = "2.0.0"
    implementation "androidx.work:work-runtime-ktx:$work_version"
    androidTestImplementation "androidx.work:work-testing:$work_version"

    // Firebase
    implementation "com.google.firebase:firebase-core:16.0.8"
    implementation 'com.google.firebase:firebase-perf:16.2.4'
    implementation 'com.google.firebase:firebase-messaging:17.5.0'
    implementation 'com.google.firebase:firebase-invites:16.1.1'

    // Glide
    def glideVersion = "4.8.0"
    implementation "com.github.bumptech.glide:glide:$glideVersion"
    kapt "com.github.bumptech.glide:compiler:$glideVersion"
    implementation "com.github.bumptech.glide:okhttp3-integration:$glideVersion"

    // Play Install Referrer Library
    implementation 'com.android.installreferrer:installreferrer:1.0'

    // Custom views
    implementation "com.github.levibostian:androidviews:0.1.4"

    // Architecture components
    def lifecycleVersion = "2.0.0"
    implementation "androidx.lifecycle:lifecycle-extensions:$lifecycleVersion"
    kapt "androidx.lifecycle:lifecycle-compiler:$lifecycleVersion"
    implementation "androidx.lifecycle:lifecycle-reactivestreams:$lifecycleVersion"
    androidTestImplementation "androidx.arch.core:core-testing:$lifecycleVersion"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycleVersion"
    implementation "androidx.lifecycle:lifecycle-reactivestreams-ktx:$lifecycleVersion"

    // Room
    def roomVersion = "2.0.0"
    implementation "androidx.room:room-runtime:$roomVersion"
    kapt "androidx.room:room-compiler:$roomVersion"
    implementation "androidx.room:room-rxjava2:$roomVersion"
    androidTestImplementation "androidx.room:room-testing:$roomVersion"

    // Koin
    def koinVersion = "2.0.0-rc-2"
    implementation "org.koin:koin-android:$koinVersion"
    implementation "org.koin:koin-android-viewmodel:$koinVersion"
    testImplementation "org.koin:koin-test:$koinVersion"

    // RxJava
    implementation "io.reactivex.rxjava2:rxandroid:2.1.0"
    implementation "io.reactivex.rxjava2:rxjava:2.2.6"
    implementation "com.f2prateek.rx.preferences2:rx-preferences:2.0.0"

    // Moshi
    def moshiVersion = "1.6.0"
    implementation "com.squareup.moshi:moshi:$moshiVersion"
    kapt "com.squareup.moshi:moshi-kotlin-codegen:$moshiVersion"
    implementation "com.squareup.moshi:moshi-adapters:$moshiVersion"

    // Retrofit
    def retrofitVersion = "2.4.0"
    implementation "com.squareup.retrofit2:retrofit:$retrofitVersion"
    implementation "com.squareup.retrofit2:converter-moshi:$retrofitVersion"
    implementation "com.squareup.retrofit2:adapter-rxjava2:$retrofitVersion"
    implementation "com.squareup.retrofit2:retrofit-mock:$retrofitVersion"

    // OkHttp
    def okHttpVersion = "3.10.0"
    implementation "com.squareup.okhttp3:logging-interceptor:$okHttpVersion"

    // Licenses fragment
    implementation 'com.github.levibostian:Android-License-Fragment:support-v4-SNAPSHOT'
    //implementation 'com.artit-k:license-fragment-support-v4:1.3.0' // Commenting out until it supports AndroidX.

    // Eventbus
    implementation "org.greenrobot:eventbus:3.0.0"

    // Crashlytics
    implementation 'com.crashlytics.sdk.android:crashlytics:2.9.9'

    // Android Debug Database
    debugImplementation 'com.amitshekhar.android:debug-db:1.0.4'

    // JUnit
    def junitVersion = "4.12"
    testImplementation "junit:junit:$junitVersion"
    androidTestImplementation "junit:junit:$junitVersion"
    androidTestImplementation 'androidx.test.ext:junit:1.1.0'

    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"
    testImplementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"
    testImplementation "org.jetbrains.kotlin:kotlin-test-junit:$kotlinVersion"

    // Truth
    def truthVersion = "0.42"
    testImplementation "com.google.truth:truth:$truthVersion"
    androidTestImplementation "com.google.truth:truth:$truthVersion"
    androidTestImplementation 'androidx.test.ext:truth:1.1.0'

    // Mockito
    def mockitoVersion = "2.24.0"
    def mockitoKotlinVersion = "2.1.0"
    testImplementation "org.mockito:mockito-core:$mockitoVersion"
    testImplementation "com.nhaarman.mockitokotlin2:mockito-kotlin:$mockitoKotlinVersion", {
        exclude group: 'org.jetbrains.kotlin'
        exclude group: 'org.mockito'
    }
    androidTestImplementation "org.mockito:mockito-android:$mockitoVersion"
    androidTestImplementation "com.nhaarman.mockitokotlin2:mockito-kotlin:$mockitoKotlinVersion", {
        exclude group: 'org.jetbrains.kotlin'
        exclude group: 'org.mockito'
    }

    // Android Testing Support Library's runner and rules
    def androidTestLibVersion = "1.1.0"
    androidTestImplementation "androidx.test:core:$androidTestLibVersion"
    androidTestImplementation "androidx.test:runner:$androidTestLibVersion"
    androidTestImplementation "androidx.test:rules:$androidTestLibVersion"

    // Espresso UI Testing
    def espressoVersion = "3.1.0"
    androidTestImplementation "androidx.test.espresso:espresso-core:$espressoVersion"
    androidTestImplementation "androidx.test.espresso:espresso-contrib:$espressoVersion"
    androidTestImplementation "androidx.test.espresso:espresso-intents:$espressoVersion"
    // androidTestImplementation "androidx.test.espresso:espresso-accessibility:$espressoVersion"
    // androidTestImplementation "androidx.test.espresso:espresso-web:$espressoVersion"
    androidTestImplementation "androidx.test.espresso.idling:idling-concurrent:$espressoVersion"
    androidTestImplementation "androidx.test.espresso:espresso-idling-resource:$espressoVersion"

    // Fastlane screengrab
    androidTestImplementation "tools.fastlane:screengrab:1.2.0"

    // Firebase screenshots
    androidTestCompile(name:'cloudtestingscreenshotter_lib', ext:'aar')
}

configurations.all {
    resolutionStrategy {
        force "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"
    }
}

apply plugin: 'com.google.gms.google-services'

// Disable running android or unit tests on release versions of the app. We don't want to especially as we only want the all-open kotlin compiler plugin to run on debug code.
project.tasks.whenTaskAdded {
    if (it.name.contains("Test") && it.name.contains("Release")) {
        it.enabled = false
    }
}